#!/usr/bin/env tclsh
#
# minimal "plumber"


fconfigure stdout -translation lf
fconfigure stderr -translation lf

set plumbfile "~/.plumb"
set plumbing_rules {}
set ws [pwd]
set attrs [dict create]

if {[info exists env(HERE)]} {
    set plumbfile $env(HERE)/lib/plumb
}

proc Usage {{code 1}} {
    puts stderr {usage: plumb [-p FILENAME] [-h] [-a NAME=VAL] [-w DIR] [--] STRING ...}
    exit $code
}

proc Plumb {pat code} {
    global plumbing_rules
    lappend plumbing_rules [list $pat $code]
}

proc GetArg {{i 1}} {
    global command_arguments
    return [lindex $command_arguments $i]
}

proc TempFile {} {
    global env
    set tmpdir "/tmp"

    if {[info exists env(TMPDIR)]} {
        set tmpdir $env(TMPDIR)
    }

    return "$tmpdir/0.[pid].[expr rand()]"
}

proc GetAttr {name {default ""}} {
    global attrs

    if {[dict exists $attrs $name]} {
        return [dict get $attrs $name]
    }

    return $default
}

proc Run args {
    exec {*}$args 2>@ stderr < /dev/null &
}

set str ""

for {set i 0} {$i < $argc} {incr i} {
    set arg [lindex $argv $i]

    switch -regex -- $arg {
        {^--?h(elp)?$} {Usage 0}
        {^-p$} {
            incr i
            set plumbfile [lindex $argv $i]
        }
        {^-w$} {
            incr i
            cd [lindex $argv $i]
        }
        {^-a$} {
            incr i
            set arg [lindex $argv $i]

            if {[regexp {^([^=]+)=(\S+)$} $arg _ n v]} {
                dict set attrs $n $v
            } else Usage
        }
        {^--$} {
            set str [concat $str [lrange $argv [expr $i + 1] end]]
            set i $argc
        }
        {^-} Usage
        default {lappend str $arg}
    }
}

source $plumbfile
close stdout
set str [join $str]

foreach r $plumbing_rules {
    set command_arguments [regexp -inline -- [lindex $r 0] $str]

    if {$command_arguments != ""} {
        set r [eval [lindex $r 1]]

        if {$r != 0} exit
    }
}

exit 1
