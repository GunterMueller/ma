#!/usr/bin/env wish
#
# gopher client for MA


set download_loc $env(HOME)/tmp
set image_viewer I
set html_viewer viewhtml
set document_viewer P
set gopher_history $env(HOME)/.gopher_history


fconfigure stdout -translation lf
fconfigure stderr -translation lf

set ma ""

if {[info exists env(MA)]} {
    set ma $env(MA)
}

if {[info exists env(TMPDIR)]} {
    set tmpdir $env(TMPDIR)/
} else {
    set tmpdir "/tmp"
}


proc Usage {{code 1}} {
    puts stderr {usage: gopher [-h] [-type TYPE] [-sel SELECTOR] HOST [-port PORT] [-stdout] [-replace FLAG]}
    exit $code
}


set sel "/"
set host ""
set port 70
set type 1
set from ""
set replace 0

for {set i 0} {$i < $argc} {incr i} {
    set arg [lindex $argv $i]

    switch -regexp -- $arg {
        {^--?h(elp)?$} {Usage 0}
        {^-sel$} {
            incr i
            set sel [lindex $argv $i]
        }
        {^-port$} {
            incr i
            set port [lindex $argv $i]
        }
        {^-type$} {
            incr i
            set type [lindex $argv $i]
        }
        {^-from$} {
            incr i
            set from [lindex $argv $i]
        }
        {^-replace$} {
            incr i
            set replace [lindex $argv $i]
        }
        {^-stdout$} {set ma ""}
        {^-} Usage
        default {
            if {$host != ""} Usage

            set host $arg
        }
    }
}

if {[catch [list socket $host $port] io]} {
    exec echo "unable to connect to $host:$port" | ma-eval
    exit
}

set f [open $gopher_history a]
puts $f "⊳$host:$port=$type/$sel⊲"
close $f

fconfigure $io -translation crlf
puts $io $sel
flush $io
set processed "$tmpdir/[expr rand()].[clock seconds]"


proc ReadFile {{dec ""}} {
    global io processed
    set data [read $io]
    close $io
    
    if {$dec != ""} {
        exec $dec << $data > $processed
    } else {
        set f [open $processed w]
        puts $f $data
        close $f
    }

    OpenFile $processed
}


proc OpenFile {fname} {
    global ma sel host port type from replace

    if {$ma != ""} {
        set title "⊳$host:$port/$sel⊲"

        if {[string first " " $title] != -1} {
            set title "'$title'"
        }

        if {$replace && $from != ""} {
            send $from UpdatePage "{$title}" "{$fname}"
            after 250
        } else {
            exec ma -post-eval "UpdatePage \"$title\" \"$fname\"" &
            after 250
        }
    } else {
        exec cat $fname >@ stdout
    }
}


proc SaveFile {prg} {
    global io processed
    fconfigure $io -translation binary -encoding binary
    set f [open $processed w]
    fconfigure $f -translation binary -encoding binary
    fcopy $io $f
    close $io
    close $f
    exec $prg $processed &
    after 250
}


proc DownloadFile {{conv ""}} {
    global io processed download_loc sel
    fconfigure $io -translation binary -encoding binary
    set f [open $processed w]
    fconfigure $f -translation binary -encoding binary
    fcopy $io $f
    close $io
    close $f
    set fname "$download_loc/[file tail $sel]"

    if {$conv != ""} {
        exec $conv < $processed > $fname
    } else {
        file rename $processed $fname
    }

    exec echo "downloaded file to $fname" | ma-eval
}


proc ProcessFile {} {
    global io processed
    set data [read $io]
    close $io
    set f [open $processed w]

    foreach line [split $data "\n"] {
        if {[regexp "^(.)(\[^\t\]*)\t(\[^\t\]*)\t(\[^\t\]+)\t(\\d+).*" $line _ type \
            str sel host port]} {
            if {$type == "i"} {
                puts $f $str
            } elseif {$type == "3"} {
                puts $f "ERROR: $str"
            } else {
                if {$port != 70} {
                    set port ":$port"
                } else {
                    set port ""
                }

                if {$type == 7} {
                    puts $f "⊳$host$port?$sel?…⊲\t$str"
                } else {
                    if {$type != 1} {
                        set type "=$type"
                    } else {
                        set type ""
                    }
    
                    if {$sel != ""} {
                        set sel "/$sel"
                    } else {
                        set sel ""
                    }
    
                    puts $f "⊳$host$port$type$sel⊲\t$str"
                }
            }
        }
    }

    close $f
    OpenFile $processed
}


switch -exact -- $type {
    g {SaveFile $image_viewer}
    p {SaveFile $image_viewer}
    P {SaveFile $image_viewer}
    d {SaveFile $document_viewer}
    6 {DownloadFile uudecode}
    9 {DownloadFile}
    5 {DownloadFile}
    h {SaveFile $html_viewer}
    1 ProcessFile
    I {SaveFile $image_viewer}
    default ReadFile
}

file delete $processed
exit
